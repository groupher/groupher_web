defmodule Helper.Utils do
  @moduledoc """
  Legacy facade for cross-domain result, configuration, map/string, and small
  normalization helpers.

  New domain-specific behavior should live with its owning context. This module
  remains the compatibility surface for widely imported helpers while focused
  implementations live under `Helper.Utils.Map` and `Helper.Utils.String`.

  Business position:

      Domain or web caller
        -> Utils
        -> normalized value / infrastructure
  """
  import Ecto.Query, warn: false
  import Helper.ErrorHandler
  alias GroupherServer.ErrorCat

  import Helper.Validator.Guards, only: [g_none_empty_str: 1]

  alias GroupherServer.Repo
  alias Helper.{Cache, Utils}

  # Map utils
  @doc "Runs `atom_values_to_upcase` through the public `Utils` boundary."
  defdelegate atom_values_to_upcase(map), to: Utils.Map
  @doc "Runs `map_key_stringify` through the public `Utils` boundary."
  defdelegate map_key_stringify(map), to: Utils.Map
  @doc "Runs `keys_to_atoms` through the public `Utils` boundary."
  defdelegate keys_to_atoms(map), to: Utils.Map
  @doc "Runs `keys_to_strings` through the public `Utils` boundary."
  defdelegate keys_to_strings(map), to: Utils.Map
  @doc "Runs `camelize_map_key` through the public `Utils` boundary."
  defdelegate camelize_map_key(map), to: Utils.Map
  defdelegate camelize_map_key(map, opt), to: Utils.Map
  @doc "Runs `snake_map_key` through the public `Utils` boundary."
  defdelegate snake_map_key(map), to: Utils.Map
  @doc "Runs `deep_merge` through the public `Utils` boundary."
  defdelegate deep_merge(left, right), to: Utils.Map
  @doc "Runs `map_atom_value` through the public `Utils` boundary."
  defdelegate map_atom_value(attrs, opt), to: Utils.Map

  # String Utils
  @doc "Runs `stringify` through the public `Utils` boundary."
  defdelegate stringify(str), to: Utils.String
  @doc "Runs `count_words` through the public `Utils` boundary."
  defdelegate count_words(str), to: Utils.String
  @doc "Runs `str_occurrence` through the public `Utils` boundary."
  defdelegate str_occurrence(string, substr), to: Utils.String

  @doc "Reads one application configuration key, or an entire section with `:all`."
  def get_config(section, key, app \\ :groupher_server)

  def get_config(section, :all, app) do
    app
    |> Application.get_env(section)
    |> case do
      nil -> ""
      config -> config
    end
  end

  def get_config(section, key, app) do
    app
    |> Application.get_env(section)
    |> case do
      nil -> ""
      config -> Keyword.get(config, key)
    end
  end

  @doc "Returns the conventional plural atom for a thread name."
  def plural(thread), do: :"#{thread}s"

  @doc "Returns the project-specific past-tense label for an action."
  def past_verb(word) do
    word_str = if is_atom(word), do: Atom.to_string(word), else: word

    case word_str do
      "upvote" -> "upvoted"
      _ -> "#{word_str}ed"
    end
  end

  @doc "Normalizes common ORM and boolean results into Groupher result tuples."
  def done(false), do: {:error, ErrorCat.custom(%{reason: :not_exist})}
  def done(true), do: {:ok, true}
  def done(nil), do: {:error, ErrorCat.custom(%{reason: :not_exist})}
  def done({n, nil}) when is_integer(n), do: {:ok, %{done: true}}
  def done(:ok), do: {:ok, :pass}
  def done([]), do: {:ok, []}
  def done(result), do: {:ok, result}
  def done(nil, :boolean), do: {:ok, false}
  def done(_, :boolean), do: {:ok, true}

  def done(nil, err_msg) when is_binary(err_msg),
    do: {:error, GroupherServer.ErrorCat.custom(err_msg)}

  def done(nil, err_msg), do: {:error, GroupherServer.ErrorCat.custom(err_msg)}
  def done({:ok, _}, with: result), do: {:ok, result}
  def done({:error, reason}, with: _result), do: {:error, normalize_error(reason)}

  def done({:ok, result}, :trans), do: result
  def done({:error, reason}, :trans), do: throw({:error, normalize_error(reason)})

  def done(nil, queryable, id),
    do:
      {:error,
       ErrorCat.custom(%{
         reason: :not_exist,
         message: not_found_formatter(queryable, id)
       })}

  def done(result, _, _), do: {:ok, result}

  defp normalize_error(%GroupherServer.ErrorCat.Error{} = error), do: error
  defp normalize_error({reason, _meta}), do: ErrorCat.custom(%{reason: reason})
  defp normalize_error(reason) when is_atom(reason), do: ErrorCat.custom(%{reason: reason})
  defp normalize_error(reason) when is_binary(reason), do: ErrorCat.custom(reason)
  defp normalize_error(reason), do: ErrorCat.custom(reason)

  # for delete_all, update_all
  # see: https://groups.google.com/forum/#!topic/elixir-ecto/1g5Pp6ceqFE
  # def done({0, nil}), do: {:error, %{done: false}}
  # def done({n, nil}, extra: extra) when is_integer(n), do: {:ok, %{done: true}}

  @doc "Normalizes a result and caches its successful value under the supplied scope."
  def done_and_cache(result, pool, scope, expire_sec: expire_sec) do
    with {:ok, res} <- done(result) do
      Cache.put(pool, scope, res, expire_sec: expire_sec)
      {:ok, res}
    end
  end

  def done_and_cache(result, pool, scope, expire_min: expire_min) do
    with {:ok, res} <- done(result) do
      Cache.put(pool, scope, res, expire_min: expire_min)
      {:ok, res}
    end
  end

  def done_and_cache(result, pool, scope) do
    with {:ok, res} <- done(result) do
      Cache.put(pool, scope, res)
      {:ok, res}
    end
  end

  @doc "Projects a domain failure into Absinthe's error result shape."
  def handle_absinthe_error(
        resolution,
        %GroupherServer.ErrorCat.Error{} = error,
        _code
      ) do
    {:error, [message: message, code: error_code]} = ErrorCat.gq_format(error)

    resolution
    |> Absinthe.Resolution.put_result({:error, message: message, extensions: %{code: error_code}})
  end

  def handle_absinthe_error(resolution, {reason, meta}, code)
      when is_integer(code) or is_binary(code) do
    message = if is_binary(meta), do: meta, else: Atom.to_string(reason)

    resolution
    |> Absinthe.Resolution.put_result({:error, message: message, extensions: %{code: code}})
  end

  def handle_absinthe_error(resolution, err_msg, code)
      when is_integer(code) or is_binary(code) do
    resolution
    |> Absinthe.Resolution.put_result({:error, message: err_msg, extensions: %{code: code}})
  end

  def handle_absinthe_error(resolution, err_msg) when is_list(err_msg) do
    # %{resolution | value: [], errors: transform_errors(changeset)}
    resolution
    # |> Absinthe.Resolution.put_result({:error, err_msg})
    |> Absinthe.Resolution.put_result(
      {:error, message: err_msg, extensions: %{code: ErrorCat.code(ErrorCat.default())}}
    )
  end

  def handle_absinthe_error(resolution, err_msg) when is_binary(err_msg) do
    resolution
    # |> Absinthe.Resolution.put_result({:error, err_msg})
    |> Absinthe.Resolution.put_result(
      {:error, message: err_msg, extensions: %{code: ErrorCat.code(ErrorCat.default())}}
    )
  end

  @doc "Repeats a scalar value and preserves the legacy single-integer-list string form."
  def repeat(times, [x]) when is_integer(x), do: to_string(for _ <- 1..times, do: x)
  def repeat(times, x), do: for(_ <- 1..times, do: x)

  @doc "Adds an integer offset, defaulting to one."
  def add(num, offset \\ 1) when is_integer(num) and is_integer(offset), do: num + offset

  @doc "Extracts one key from each map in a list while preserving order."
  def pick_by(source, key) when is_list(source) and is_atom(key) do
    Enum.reduce(source, [], fn t, acc ->
      acc ++ [Map.get(t, key)]
    end)
  end

  @doc "Returns the canonical empty pagination payload."
  def empty_pagi_data do
    %{entries: [], total_count: 0, page_size: 0, total_pages: 1, page_number: 1}
  end

  @doc "Checks whether a string length or integer is greater than or equal to a target."
  @spec large_than(String.t() | integer(), integer()) :: true | false
  def large_than(value, target) when is_binary(value) and is_integer(target) do
    String.length(value) >= target
  end

  def large_than(value, target) when is_integer(value) and is_integer(target) do
    value >= target
  end

  @spec large_than(String.t() | integer(), integer(), :no_equal) :: true | false
  def large_than(value, target, :no_equal) when is_binary(value) and is_integer(target) do
    String.length(value) > target
  end

  def large_than(value, target, :no_equal) when is_integer(value) and is_integer(target) do
    value > target
  end

  @spec less_than(String.t() | integer(), integer()) :: true | false
  @doc "Runs `less_than` through the public `Utils` boundary."
  def less_than(value, target) when is_binary(value) and is_integer(target) do
    String.length(value) <= target
  end

  def less_than(value, target) when is_integer(value) and is_integer(target) do
    value <= target
  end

  @spec less_than(String.t() | integer(), integer(), :no_equal) :: true | false
  def less_than(value, target, :no_equal) when is_binary(value) and is_integer(target) do
    String.length(value) < target
  end

  def less_than(value, target, :no_equal) when is_integer(value) and is_integer(target) do
    value < target
  end

  @doc """
  convert struct to normal map and remove :id field
  """

  # def strip_struct(struct) when is_struct(struct) do
  #   struct |> Map.from_struct() |> Map.delete(:id) |> Map.delete(:__meta__)
  # end
  # def strip_struct(map) when is_map(map), do: map

  def strip_struct(%DateTime{} = datetime), do: datetime

  def strip_struct(data) when is_map(data) do
    # 如果是 struct（但不是 DateTime），先转换为 map
    data =
      if Map.has_key?(data, :__struct__) do
        Map.from_struct(data) |> Map.delete(:id)
      else
        data
      end

    # 递归处理所有值
    Enum.reduce(data, %{}, fn {k, v}, acc ->
      Map.put(acc, k, strip_struct(v))
    end)
  end

  def strip_struct(data) when is_list(data) do
    Enum.map(data, &strip_struct/1)
  end

  def strip_struct(data), do: data

  @doc """
  get upcase name of a module, most used for store thread in DB
  """
  def module_to_upcase(module) do
    module |> Module.split() |> List.last() |> String.upcase()
  end

  @doc """
  get atom name of a module
  """
  def module_to_atom(%{__struct__: module_struct}) do
    module_struct
    |> Module.split()
    |> List.last()
    |> String.downcase()
    |> String.to_atom()
  end

  def module_to_atom(module_struct) do
    module_struct |> struct |> module_to_atom
  rescue
    _ -> nil
  end

  @doc "Converts the input to upcase at the `Utils` boundary."
  def to_upcase(v) when is_atom(v), do: v |> to_string |> String.upcase()
  def to_upcase(v) when is_binary(v), do: v |> String.upcase()
  def to_upcase(_), do: nil

  @doc "Runs `uid` through the public `Utils` boundary."
  def uid(str_len \\ 5) do
    Nanoid.generate(str_len, "abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLMNOPQRSTUVWXYZ0123456789")
  end

  @doc "html uniq id generator for editor.js"
  @spec uid(:html, map) :: String.t()
  def uid(:html, %{"id" => id}) when g_none_empty_str(id), do: id

  def uid(:html, _) do
    # number is invalid for html id(if first letter)
    Nanoid.generate(5, "abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLMNOPQRSTUVWXYZ")
  end

  @doc """
  get host from url for config, like
  postgres://default:blajdife@example-db-host.aws.neon.tech:5432/verceldb?sslmode=require
  => example-db-host
  """
  def get_host_from_url(url) do
    URI.parse(url).host
  end

  # Repo.transaction will rewrite error to {:error, :rollback}, so if we want to return error with
  # details context, need use try catch
  @doc "Runs `use_transaction` through the public `Utils` boundary."
  def use_transaction(fun) do
    Repo.transaction(fn ->
      case fun.() do
        {:ok, result} -> result
        {:error, reason} -> throw({:error, reason})
        value -> value
      end
    end)
  catch
    {:error, reason} -> {:error, reason}
  end
end
